plugins {
	id 'java'
	id 'groovy'
	id 'jacoco'
	id 'checkstyle'
	id 'codenarc'
	id 'org.springframework.boot' version '3.0.6'
	id 'io.spring.dependency-management' version '1.1.0'
}

group = 'com.personal'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('testcontainersVersion', "1.18.0")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-groovy-templates'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// swagger
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.15'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// postgres
	runtimeOnly 'org.postgresql:postgresql'

	// spock
	testImplementation 'org.spockframework:spock-spring:2.4-M1-groovy-4.0'
	testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'

	// wiremock
	implementation 'org.springframework.cloud:spring-cloud-contract-wiremock:4.0.1'

	// testcontainers
	testImplementation 'org.testcontainers:spock:1.17.6'
	testImplementation "org.testcontainers:postgresql:1.17.6"
	testImplementation 'com.github.dasniko:testcontainers-keycloak:2.5.0'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter'

	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
}

dependencyManagement {
	imports {
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

sourceSets {
	integrationTest {
		groovy {
			compileClasspath += sourceSets.main.output + sourceSets.test.output
			runtimeClasspath += sourceSets.main.output + sourceSets.test.output
			srcDir 'src/integration-test/groovy'
		}
		resources.srcDir 'src/integration-test/resources'
	}
}

configurations.integrationTestImplementation.extendsFrom(configurations.testImplementation)
configurations.integrationTestRuntimeOnly.extendsFrom(configurations.testRuntimeOnly)

task integrationTest(type: Test) {
	group = 'verification'
	useJUnitPlatform()

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath

	shouldRunAfter 'test'
}

bootJar {
	archiveFileName = "hotel_booking_backend.jar"
}

test {
	finalizedBy jacocoTestReport
    exclude "**/*FunctionalSpec*"
}

tasks.withType(Test) {
	useJUnitPlatform()
}

apply from: rootProject.file('gradle/jacoco.gradle')



checkstyle {
	toolVersion = '10.3.1'
	configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

codenarc {
	toolVersion = '3.1.0'
	configFile = file("${rootDir}/config/codenarc/codenarc.groovy")
	maxPriority3Violations = Integer.MAX_VALUE
}

checkstyleMain {
	source = 'src/main/java'
}

checkstyleTest {
	source = 'src/test/groovy'
}


